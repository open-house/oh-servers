#!/usr/bin/perl
use strict;
use warnings;
use 5.010;
use LWP::UserAgent;
use JSON;
use autodie;
use Net::Ping;
use MIME::Base64;

#
# VARIABLES
#

# Environment variables shared vith nova via .novarc
system ". $ENV{HOME}/.novarc";    # source nova config
my $USERNAME = $ENV{OS_USERNAME};
my $API_KEY  = $ENV{OS_PASSWORD};

#
# FUNCTIONS
#

# Create a user agent object - common for several functions
use LWP::UserAgent;
my $ua = LWP::UserAgent->new;
$ua->agent("MyApp/0.1");

sub authenticate {

    # Create a request
    my $req =
      HTTP::Request->new(
        POST => 'https://identity.api.rackspacecloud.com/v2.0/tokens' );
    $req->content_type('application/json');

    my $json = {
        'auth' => {
            'RAX-KSKEY:apiKeyCredentials' => {
                'apiKey'   => $API_KEY,
                'username' => $USERNAME,
            }
        }
    };

    $req->content( encode_json $json );

    # Pass request to the user agent and get a response back
    my $res = $ua->request($req);

    my ( $account, $token );

    # Check the outcome of the response
    if ( $res->is_success ) {
        my $perl_ds = decode_json $res->content;
        $account = $perl_ds->{'access'}->{'token'}->{'tenant'}->{'id'};
        $token   = $perl_ds->{'access'}->{'token'}->{'id'};
        return $account, $token;
    }
}

sub create_server {
    my ( $account, $token, $name ) = @_;

    # Get my SSH public key
    open my ($ssh_key_fh), "$ENV{HOME}/.ssh/id_rsa.pub";
    chomp( my $ssh_key = <$ssh_key_fh> );
    close $ssh_key_fh;

    my $server = {
        'server' => {
            'imageRef'  => '8a3a9f96-b997-46fd-b7a8-a9e740796ffd',
            'flavorRef' => '2',
            'name'      => $name,
            'metadata' =>
              { 'My Server Name' => 'Ubuntu 12.10 (Quantal Quetzal)' },
            'personality' => [
                {
                    'path'     => '/root/.ssh/authorized_keys',
                    'contents' => encode_base64 $ssh_key,
                }
            ],
        }
    };

    my $req = HTTP::Request->new( POST =>
          "https://dfw.servers.api.rackspacecloud.com/v2/$account/servers" );
    $req->header(
        "Content-Type"      => "application/json",
        "X-Auth-Token"      => $token,
        "X-Auth-Project-Id" => "test-project"
    );
    $req->content( encode_json $server);
    my $res = $ua->request($req);

    my ( $pass, $id );

    # Check the outcome of the response
    if ( $res->is_success ) {
        my $perl_ds = decode_json $res->content;
        $pass = $perl_ds->{server}->{adminPass};
        $id   = $perl_ds->{server}->{'id'};
        return $pass, $id;
    } else {
        #print STDERR ">>>", $res->status_line, "<<<\n";
    }
}

sub get_server_ip {
    my ( $account, $token, $id ) = @_;

    my $perl_ds = list_servers( $account, $token );
    for my $server ( @{ $perl_ds->{servers} } ) {
        if ( $server->{id} eq $id ) {
            return $server->{accessIPv4};
        }
    }
}

sub get_server_id {
    my ( $account, $token, $hostname ) = @_;

    my $perl_ds = list_servers( $account, $token );
    for my $server ( @{ $perl_ds->{servers} } ) {
        if ( $server->{name} eq $hostname ) {
            return $server->{id};
        }
    }

    return 0; # meaning noexistent server
}

sub list_servers {
    my ( $account, $token ) = @_;

    my $req =
      HTTP::Request->new( GET =>
"https://dfw.servers.api.rackspacecloud.com/v2/$account/servers/detail"
      );
    $req->header( "X-Auth-Token" => "$token" );
    my $res = $ua->request($req);

    my ( $ip, $status, $id, $name );

    if ( $res->is_success ) {
        return decode_json $res->content;    # Perl data structure
    } else {
        return undef;
    }
}

sub delete_server {
    my ( $account, $token, $hostname ) = @_;

    my $id = get_server_id( $account, $token, $hostname );

    my $req =
      HTTP::Request->new( DELETE =>
          "https://dfw.servers.api.rackspacecloud.com/v2/$account/servers/$id"
      );
    $req->header( "X-Auth-Token" => "$token" );
    my $res = $ua->request($req);

    if ( $res->is_success ) {
        #print "Server '$hostname' with id '$id' will be deleted\n";
    } else {
        #print STDERR $res->status_line, "\n";
        print STDERR "Server '$hostname' with id '$id' not found\n";
    }
}

#
# MAIN
#

my $cmd      = shift;
my $hostname = shift;
die "Usage: $0 create | delete <hostname>\n"
  unless defined $cmd and defined $hostname;

my ( $account, $token ) = authenticate();

given ($cmd) {
    when (/^create$/) {
        my ( $pass, $id ) = create_server( $account, $token, $hostname );

        my $n = 0;
        while (1) {

            # redo comes here
            my $perl_ds = list_servers( $account, $token );
            my $server = ${ $perl_ds->{servers} }[ $n++ ];
            unless ($server->{id} eq $id
                and $server->{'OS-EXT-STS:vm_state'} eq 'active' )
            {
                print STDERR $server->{name} . " - "
                  . $server->{'OS-EXT-STS:vm_state'} . " ("
                  . $server->{progress} . " %)\n";
                sleep 10;
                $n = 0 if $n >= @{ $perl_ds->{servers} };
                redo;
            }
            last;    # exit infinite while loop
        }

        # Get IP address (not ready immediately after VM is active?)
        my $ip = 0;
        until ( $ip =~ /^\d+\.\d+\.\d+\.\d+$/ ) {
            $ip = get_server_ip( $account, $token, $id );
            sleep 3;
        }

        # Make sure the host is up
        my $p = Net::Ping->new("syn");
        sleep(3) until $p->ping($ip);
        $p->close();

        # the only print to STDOUT - will be use by the wrapper script!
        print $ip;
    }
    when (/^delete$/) { delete_server( $account, $token, $hostname ) }
    default { print STDERR "Uknown command '$cmd'.\n" }
}

